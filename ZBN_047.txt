XLSX to Internal table :

DATA : lv_rc     TYPE i,
         it_files  TYPE filetable,
         lv_action TYPE i.


  cl_gui_frontend_services=>file_open_dialog( EXPORTING
                                             file_filter = |xlsx (*.xlsx)\|*.xlsx\|{ cl_gui_frontend_services=>filetype_all }|
                                             CHANGING
                                                 file_table = it_files
                                                  rc = lv_rc
                                                  user_action = lv_action ).

  IF lv_action = cl_gui_frontend_services=>action_ok.

    IF lines( it_files ) > 0.

      DATA : lv_filesize TYPE w3param-cont_len,
             lv_filetype TYPE w3param-cont_type,
             it_bin_data TYPE w3mimetabtype.

      cl_gui_frontend_services=>gui_upload( EXPORTING
                                            filename = |{ it_files[ 1 ]-filename }|
                                            filetype = 'BIN'
                                            IMPORTING
                                              filelength = lv_filesize
                                              CHANGING
                                                data_tab = it_bin_data ).

      DATA(lv_bin_data) = cl_bcs_convert=>solix_to_xstring( it_solix = it_bin_data ).

      DATA(o_excel) = NEW cl_fdt_xl_spreadsheet( document_name = CONV #( it_files[ 1 ]-filename )
                                                 xdocument = lv_bin_data ).

      DATA : it_worksheet_name TYPE if_fdt_doc_spreadsheet=>t_worksheet_names.

      o_excel->if_fdt_doc_spreadsheet~get_worksheet_names( IMPORTING worksheet_names = it_worksheet_name ) .

      IF lines( it_worksheet_name ) > 0.

        DATA(o_worksheet_itab) = o_excel->if_fdt_doc_spreadsheet~get_itab_from_worksheet( it_worksheet_name[ 1 ] ).

        ASSIGN o_worksheet_itab->* TO FIELD-SYMBOL(<worksheet>).

        cl_demo_output=>write_data( <worksheet> ).

        DATA(lv_html) = cl_demo_output=>get( ).

        cl_abap_browser=>show_html( EXPORTING
                                    title = 'Excel Worksheet'
                                    html_string = lv_html
                                    container = cl_gui_container=>default_screen ).
        WRITE: space.
      ENDIF.
    ENDIF.
  ENDIF.